#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#########################################
# Project : Tango - Save directory
# Created by : FBU
# Created at : 11/07/2011
# Modified by :
# Modified at :
# Modify reason :
#########################################


#-- Sauvegarde des données du serveur sur disque
SaveDataAll;

#-- retraitement du nom (vide ou espace si lancement par chores)
IF( Name@<>'' & Name@<>' ');
   Name='_' | Name;
ELSE;
   Name='';
ENDIF;

#-- Récupération de l'emplacement du fichier de commande
TMP_DIR=CellGetS('z_Admin_Param','REP_TEMP','STR_VAR1') ;

IF( FileExists(TMP_DIR | '\save.cmd')=1);
   ProcessQuit;
ENDIF;

#-- Répertoire de destination de sauvegarde
DIR=CellGetS('z_Admin_Param','REP_SAVE','STR_VAR1');

#-- Répertoire où se trouvent les données à sauvegarder
DATA_DIR='"' | CellGetS('z_Admin_Param','REP_TM1DATA','STR_VAR1') | '"';

#-- Permet de supprimer le délimiteur du champ dans le batch
DatasourceASCIIQuoteCharacter='';

#-- Ecriture de la liste des fichiers à exclure de la sauvegarde (fichiers config, licence, fichiers erreur...)
ASCIIOUTPUT(TMP_DIR | '\Exclude.txt','tm1s');
ASCIIOUTPUT(TMP_DIR | '\Exclude.txt','TM1ProcessError');


#-- Conservation du 1er au dernier jour du mois en glissant + matin ou après-midi selon l'heure
#Mantis 1084 - Conservation uniquement des 7 derniers jours (ISO avec Véolia France)
#JOUR=TIMST(NOW,'\d');
JOUR = MOD(DAYNO(TODAY) + 21915 , 7 );

IF( JOUR = 1 );
   JOUR_RET = 'LUNDI';
ELSEIF( JOUR = 2 );
   JOUR_RET = 'MARDI';
ELSEIF( JOUR = 3 );
   JOUR_RET = 'MERCREDI';
ELSEIF( JOUR = 4 );
   JOUR_RET = 'JEUDI';
ELSEIF( JOUR = 5 );
   JOUR_RET = 'VENDREDI';
ELSEIF( JOUR = 6 );
   JOUR_RET = 'SAMEDI';
ELSE;
   JOUR_RET = 'DIMANCHE';
ENDIF;

H=NUMBR(TIMST(NOW,'\h'));
IF(H<14);
   T='AM';
ELSE;
   T='PM';
ENDIF;
#IF( Name @= '' );
#    Name = 'BKP';
#ELSE;
#    Name = Name;
#ENDIF;

JOUR_RET = JOUR_RET | '-' | T| Name;


#-- Ecriture du fichier de commande
#-- Répertoire pour destination des sauvegardes
ASCIIOUTPUT(TMP_DIR | '\save.cmd','Set vSave=' | DIR);
#-- Répertoire a sauvegarder
ASCIIOUTPUT(TMP_DIR | '\save.cmd','Set vData=' | DATA_DIR);
#-- Emplacement de la liste des fichiers a exclure
ASCIIOUTPUT(TMP_DIR | '\save.cmd','Set vExclude=' | TMP_DIR | '\Exclude.txt');

#-- Suppression/creation du repertoire de sauvegarde
ASCIIOUTPUT(TMP_DIR | '\save.cmd','Rd /S/Q %vSave%\' | JOUR_RET);
ASCIIOUTPUT(TMP_DIR | '\save.cmd','Md %vSave%\' | JOUR_RET);
ASCIIOUTPUT(TMP_DIR | '\save.cmd','Md %vSave%\' | JOUR_RET | '\TM1Data');

#-- Recopie de la base de donnees
ASCIIOUTPUT(TMP_DIR | '\save.cmd','xcopy /Exclude:%vExclude% /E/C/q  %vData% %vSave%\' | JOUR_RET | '\TM1Data\ ');

#-- Auto-suppression du batch et du fichier des exceptions
ASCIIOUTPUT(TMP_DIR | '\save.cmd','del ' | TMP_DIR | '\Exclude.txt');
ASCIIOUTPUT(TMP_DIR | '\save.cmd','del ' | TMP_DIR | '\save.cmd');
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****



ExecuteCommand('cmd /c " Call ' | TMP_DIR | '\save.cmd  && exit"',1);
#endregion