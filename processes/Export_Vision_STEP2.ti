#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#########################################
# Project : Tango - Export data for Vision STEP2 (création d'un fichier pour le pays défini en paramètre)
# Created by : RSJC
# Created at : 13/06/2016
# Modified by : BTA 
# Modified at : 03/11/2016
# Modify reason : Export phase selected by country
#########################################

zCube = 'RP_PL';
V_PROCESS_NAME = GETPROCESSNAME();


###########################################################################
#	BEGIN: DEBUG FILES 
###########################################################################
IF(pDEBUG<>0);
	
	V_TIMESTAMP=TIMST(NOW, 'Ymd_his');
	V_DBG_FOLDER=CELLGETS('Z_ADMIN_PARAM','REP_TEMP','STR_VAR1');
	#--REM= Definition of the names of the files of LOG for the DEBUG mode ( one file per process tab).
	V_DBG_FILE_PROLOG=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_PROLOG.txt';
	V_DBG_FILE_METADATA=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_METADATA.txt';
	V_DBG_FILE_DATA=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_DATA.txt';
	V_DBG_FILE_EPILOG=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_EPILOG.txt';

ENDIF;

###########################################################################
#	END: DEBUG FILES 
###########################################################################


V_TIMESTAMP=TIMST(NOW, '\Y\m\d_\h\i\s');
V_VIEW = 'TMP_VIEW_' | zCube | '_' | V_PROCESS_NAME | '_' | pPeriod | '_' | V_TIMESTAMP;
V_SUBSET = 'TMP_SUBSET_' | zCube | '_' |  V_PROCESS_NAME | '_' | pPeriod | '_' | V_TIMESTAMP;




###  Création de la vue V_VIEW
IF(ViewExists(zCube,V_VIEW)=1);
	ViewDestroy(zCube,V_VIEW);
ENDIF;

ViewCreate(zCube,V_VIEW);


#-- Création du sous-ensemble Legal_Organization
IF( SubsetExists('Legal_Organization' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Legal_Organization' , V_SUBSET);
ENDIF;

SubsetCreateByMDX(V_SUBSET, '{FILTER( {TM1FILTERBYLEVEL( {TM1SUBSETALL( [Legal_Organization] )}, 0)}, [Legal_Organization].[Country_entity] = "' | pCountry | '")}');
V_SUBSET_SIZE = SubsetGetSize( 'Legal_Organization', V_SUBSET);
SubsetElementInsert( 'Legal_Organization', V_SUBSET, DIMNM( 'Legal_Organization', 1), V_SUBSET_SIZE + 1);
SubsetElementDelete( 'Legal_Organization', V_SUBSET, V_SUBSET_SIZE + 1);

ViewSubsetAssign( zCube, V_VIEW, 'Legal_Organization', V_SUBSET);


#-- Création du sous-ensemble Activity
IF( SubsetExists( 'Activity' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Activity' , V_SUBSET);
ELSE;
    SubsetCreate( 'Activity' , V_SUBSET);
ENDIF;
i=1;
WHILE ( i < DIMSIZ( 'Activity' ) +1);
    ElemP = DIMNM ( 'Activity' , i );
    IF( ELLEV( 'Activity' , ElemP) = 0 & ELISANC('Activity', 'ACT_PMM', Elemp)=1);
        SubsetElementInsert( 'Activity' , V_SUBSET, ElemP , 1 ) ;
    ENDIF;
 i=i+1;
END;

ViewSubsetAssign( zCube, V_VIEW, 'Activity' , V_SUBSET);


#-- Création du sous-ensemble Currency
IF( SubsetExists( 'Currency' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Currency' , V_SUBSET);
ELSE;
    SubsetCreate( 'Currency' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Currency' , V_SUBSET, 'LCL' , 1 ) ;
ViewSubsetAssign( zCube, V_VIEW, 'Currency' , V_SUBSET);



#-- Création du sous-ensemble Gaap
IF( SubsetExists( 'Gaap' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Gaap' , V_SUBSET);
ELSE;
    SubsetCreate( 'Gaap' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Gaap' , V_SUBSET, 'Total_conso', 1 ) ;

ViewSubsetAssign( zCube, V_VIEW, 'Gaap' , V_SUBSET);


#-- Création du sous-ensemble Integration_Rate
IF( SubsetExists( 'Integration_Rate' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Integration_Rate' , V_SUBSET);
ELSE;
    SubsetCreate( 'Integration_Rate' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Integration_Rate' , V_SUBSET, 'NO_APP' , 1 ) ;

ViewSubsetAssign( zCube, V_VIEW, 'Integration_Rate' , V_SUBSET);


#-- Création du sous-ensemble Management_Organization
IF( SubsetExists('Management_Organization' , V_SUBSET) =1 );
    SubsetDeleteAllElements('Management_Organization' , V_SUBSET);
ENDIF;

#Sélection uniquement des enfants des pays dont l'attribut PMM_Country = "PMM_Country"
SubsetCreateByMDX(V_SUBSET,'{TM1FILTERBYLEVEL( {TM1DRILLDOWNMEMBER( {EXCEPT( {FILTER( {TM1SUBSETALL( [Management_Organization] )}, [Management_Organization].[PMM_Country] = "PMM_Country")}, {[Management_Organization].[FR] }) }, ALL, RECURSIVE )}, 0)}');
V_SUBSET_SIZE = SubsetGetSize( 'Management_Organization', V_SUBSET);
SubsetElementInsert( 'Management_Organization', V_SUBSET, DIMNM( 'Management_Organization', 1), V_SUBSET_SIZE + 1);
SubsetElementDelete( 'Management_Organization', V_SUBSET, V_SUBSET_SIZE + 1);

ViewSubsetAssign( zCube, V_VIEW, 'Management_Organization', V_SUBSET);



#-- Création du sous-ensemble Period
IF( SubsetExists( 'Period' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Period' , V_SUBSET);
ELSE;
    SubsetCreate( 'Period' , V_SUBSET);
ENDIF;

#Paramètre de la période
IF(pPeriod@='Default');
	V_PERIOD_SRC = CellGetS( 'z_Admin_Param' , 'MONTH_ACTUAL' , 'STR_VAR1') | '_YTD';
ELSE;
	V_PERIOD_SRC = pPeriod;
ENDIF;

SubsetElementInsert( 'Period' , V_SUBSET, V_PERIOD_SRC , 1 ) ;
  
ViewSubsetAssign( zCube, V_VIEW, 'Period' , V_SUBSET);



#-- Création du sous-ensemble Phase
IF( SubsetExists( 'Phase' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Phase' , V_SUBSET);
ELSE;
    SubsetCreate( 'Phase' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Phase' , V_SUBSET, 'ACT_TOT', 1 ) ;


# -- BTA 03/11/2016 - Récupérer la phase à exporter pour le pCountry
pPhase = CellGetS( '}ElementAttributes_Country_List_VISION', pCountry, 'Phase_to_export_to_Vision' ); 

# -- Par défaut, si la phase n'est pas FC1 ou FC2 alors BGY
IF( pPhase @= 'Forecast 1' ); 
	pPhase = 'FC_1_VC' ; 
ELSEIF( pPhase @= 'Forecast 2' );
	pPhase = 'FC_2_VC' ; 
ELSE;
	pPhase = 'BUDG_VC' ; 
ENDIF;

SubsetElementInsert( 'Phase' , V_SUBSET, pPhase, 1 ) ;

ViewSubsetAssign( zCube, V_VIEW, 'Phase' , V_SUBSET);


#-- Création du sous-ensemble Indicator
IF( SubsetExists( 'Indicator' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Indicator' , V_SUBSET);
ELSE;
    SubsetCreate( 'Indicator' , V_SUBSET);
ENDIF;
i=1;
WHILE( i < DIMSIZ( 'Indicator' ) + 1 ) ;
    ElemP = DIMNM( 'Indicator' , i );
    IF(ATTRS('Indicator',ElemP, 'VISION')  @= 'VISION'); 
        SubsetElementInsert( 'Indicator' , V_SUBSET, ElemP , 1 ) ;
    ENDIF;
i=i+1;
END;

ViewSubsetAssign( zCube, V_VIEW, 'Indicator' , V_SUBSET);


#-- Mise à jour des paramètres de la vue
ViewExtractSkipZeroesSet ( zCube, V_VIEW, 1 );
ViewExtractSkipRuleValuesSet ( zCube, V_VIEW, 0 );
ViewExtractSkipCalcsSet ( zCube, V_VIEW, 0 );

DataSourceNameForServer = zCube;
DataSourceCubeView = V_VIEW;


DatasourceASCIIQuoteCharacter='';
DatasourceASCIIDelimiter=';';
DatasourceASCIIDecimalSeparator='.';


vHeader=1;
#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****

#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****

V_Country = ATTRS('Legal_Organization',V_Legal_Organization,'Country_entity');
V_Folder = ' "' | CellGets('Z_ADMIN_PARAM','REP_OUTPUT','STR_VAR1') | '\Vision\' | V_Country |'"';


# create country folder 
sCommand = 'cmd /c "md ' | V_Folder | ' " ';
ExecuteCommand ( sCommand, 1 );



#Fichier à exporter
V_TXT_FILE=CellGets('Z_ADMIN_PARAM','REP_OUTPUT','STR_VAR1') | '\Vision' | '\' | V_Country | '\' | V_Country |  '_' | V_TIMESTAMP|'.csv';




#### Set the headers in the output file ####

IF (vHeader=1);

ASCIIOUTPUT(V_TXT_FILE, 'Country', 'Period', 'Phase', 'Management_Organization', 'Activity_Business Model', 'Indicator', 'Value', V_TIMESTAMP);

vHeader=0;

ENDIF;




#Données cumulées mais période extraite sans _YTD

IF( SUBST(V_PERIOD,LONG(V_PERIOD)-3,4)@='_YTD');

	V_Period_Final=SUBST(V_PERIOD,1,LONG(V_PERIOD)-4);
ENDIF;



# Phase ACTUAL = realise
IF (V_Phase@='ACT_TOT');V_Phase_Final = 'Realise';
ELSEIF (V_Phase@='BUDG_VC');V_Phase_Final = 'Budget';
ELSEIF (V_Phase@='FC_1_VC');V_Phase_Final = 'ESTIME1_VDEF';
ELSEIF (V_Phase@='FC_2_VC');V_Phase_Final = 'ESTIME2_VDEF';
ELSE;
     V_Phase_Final = V_PHASE;
ENDIF;



ASCIIOUTPUT(V_TXT_FILE,V_Country, V_Period_Final, V_Phase_Final, V_Management_Organization, V_Activity, V_Indicator, V_Value, V_TIMESTAMP);
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****


IF(pDEBUG=0);


IF(ViewExists(zCube,V_VIEW)=1);ViewDestroy(zCube,V_VIEW);ENDIF;

IF(SubsetExists('Activity',V_SUBSET)=1);SubsetDestroy('Activity',V_SUBSET);ENDIF;
IF(SubsetExists('Currency',V_SUBSET)=1);SubsetDestroy('Currency',V_SUBSET);ENDIF;
IF(SubsetExists('Gaap',V_SUBSET)=1);SubsetDestroy('Gaap',V_SUBSET);ENDIF;
IF(SubsetExists('Integration_Rate',V_SUBSET)=1);SubsetDestroy('Integration_Rate',V_SUBSET);ENDIF;
IF(SubsetExists('Legal_Organization',V_SUBSET)=1);SubsetDestroy('Legal_Organization',V_SUBSET);ENDIF;
IF(SubsetExists('Management_Organization',V_SUBSET)=1);SubsetDestroy('Management_Organization',V_SUBSET);ENDIF;
IF(SubsetExists('Period',V_SUBSET)=1);SubsetDestroy('Period',V_SUBSET);ENDIF;
IF(SubsetExists('Phase',V_SUBSET)=1);SubsetDestroy('Phase',V_SUBSET);ENDIF;
IF(SubsetExists('Indicator',V_SUBSET)=1);SubsetDestroy('Indicator',V_SUBSET);ENDIF;



ENDIF;
#endregion