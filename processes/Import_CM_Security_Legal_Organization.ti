#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#########################################
# Project : Tango - Core Model
# Created by : ASZ - Anone
# Created at : 02/05/2016
# Modified by : BTA 
# Reason : Refonte de la sécurité + revue de la vue source qui était statique + ajout des commentaires
# Date : 12/01/2017
# Modified by : MTA
# Reason : Ajout Opérationsadmin dnas la condition des droits
# Date :25/04/2022
#########################################

#################################################################################
#                                                               Déclaration des variables
#################################################################################

zCube = '}ElementSecurity_Legal_Organization';
Dim_LO = 'Legal_Organization';
zSubset = GetProcessName() ; 

Subset_MDX_R = 'z_mdx_legal_organization_R';
Subset_MDX_W = 'z_mdx_legal_organization_W';


#################################################################################
#                                                               Clear Cube
#################################################################################

CubeClearData( zCube ) ;



#################################################################################
#                                                               Subset Source 
#################################################################################

# -- Sous ensemble Source Groups
zDim = '}Groups' ;
SubsetDestroy( zDim , zSubset );
SubsetCreate( zDim , zSubset );
SubsetElementInsert( zDim , zSubset , 'Corporate_Plus' , 1 );
SubsetElementInsert( zDim , zSubset , 'Financial_Controller' , 1 );
SubsetElementInsert( zDim , zSubset , 'Performance_Corporate' , 1 );
SubsetElementInsert( zDim , zSubset , 'User_Holding' , 1 );
## 10/06/2021 RSJC Ajout droits User_STrategy
SubsetElementInsert( zDim , zSubset , 'User_Strategy' , 1 );

i = 1 ; 
iMax = DimSiz( zDim ); 
While( i <= iMax ) ; 

	vElem = DimNm( zDim , i ); 

	IF( SCAN( 'R_', vElem ) <> 0 % SCAN( 'W_' , vElem ) <> 0 ); 
		SubsetElementInsert( zDim , zSubset , vElem , 1 ) ; 
	ENDIF;

	i = i + 1; 

End;


DatasourceDimensionSubset = zSubset ;



#################################################################################
#                                                               Elément NA visible par tous les groupes en lecture
#################################################################################


# Boucle sur les groups
zDim = '}Groups' ;
i = 1 ; 
iMax = DimSiz( zDim ); 
While( i <= iMax ); 

	vElem = DimNm( zDim , i ); 

	IF( vElem @<> 'Admin' & vElem @<> 'DataAdmin' & vElem @<> 'SecurityAdmin' & vElem @<> 'OperationsAdmin'  ); 
		CellPutS( 'READ' , zCube , 'NA' , vElem ) ; 
	ENDIF;

	i = i + 1; 
End;

#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****

# -- Ajout des modifications liées à la refonte de la sécurité


# -- Pour tous les éléments de la dimension Legal Organization et les 2 groupes Corporate Plus et Financial Controller, renseigner le READ
## 10/06/2021 RSJC Ajout droits User_STrategy
# IF( Groups @= 'Corporate_Plus' % Groups @= 'Financial_Controller' % Groups @= 'Performance_Corporate' );
IF( Groups @= 'Corporate_Plus' % Groups @= 'Financial_Controller' % Groups @= 'Performance_Corporate' % Groups @= 'User_Strategy' );

	i = 1 ; 
	iMax = DimSiz ( Dim_LO ); 

	While( i <= iMax ); 

		# -- Get de l'élément courant
		vElem = DimNm( Dim_LO, i ); 

		# -- Data Input
		CellPutS( 'WRITE' , zCube , vElem, Groups ); 		

		# -- Incrémentation du compteur 
		i = i + 1; 

	End;

	ItemSkip;
ENDIF;

# -- Pour le groupe User Holding, donner accès aux enfants de VTD Corporate
IF( Groups @= 'User_Holding' ); 

	i = 1 ; 
	iMax = DimSiz ( Dim_LO ); 

	While( i <= iMax ); 

		# -- Get de l'élément courant
		vElem = DimNm( Dim_LO, i ); 

		# -- Data Input
		IF( ELISANC( DIM_LO, 'VTD_Corporate' , vElem ) = 1 % vElem @= 'VTD_Corporate' ); 
			CellPutS( 'WRITE' , zCube , vElem, Groups ); 		
		ENDIF;

		# -- Incrémentation du compteur 
		i = i + 1; 

	End;

	ItemSkip;

ENDIF;


# RSJC Ajout 22/11/2019
# -- Les groupes W_North_America et W_USA et W_CAN auront accès à Cityway_Corp
IF( Groups @= 'W_North_America' % Groups @= 'W_USA' % Groups @= 'W_CAN' ); 
	CellPutS( 'WRITE' , zCube, 'Cityway_Corp' , Groups ); 
ENDIF;



IF( SUBST( Groups , 1 , 2 ) @= 'R_' ); 

	zLO_R = SUBST( Groups , 3 , LONG(Groups) );

	# -- Si l'élément n'existe pas alors on skip
	IF( DIMIX( Dim_LO , zLO_R ) = 0 );
		ITEMSKIP;
	ELSE;
		CellPutS('READ', zCube, zLO_R, Groups );
	ENDIF;

	IF( Subsetexists( Dim_LO, Subset_MDX_R ) = 1 );
		Subsetdestroy( Dim_LO , Subset_MDX_R );
	ENDIF;

	SubsetCreatebyMDX ( Subset_MDX_R, '{TM1DRILLDOWNMEMBER( {TM1FILTERBYPATTERN( {TM1SUBSETALL( [Legal_Organization] )}, "'|zLO_R|'")}, ALL, RECURSIVE )}' );

	nSize_R_LO = SubsetGetSize( 'Legal_Organization' , Subset_MDX_R );

	j=1;
	WHILE( j <= nSize_R_LO );
		Elem_R_LO = SubsetGetElementName( Dim_LO , Subset_MDX_R , j );
		CellPutS( 'READ' , zCube , Elem_R_LO , Groups ) ; 
		j = j + 1;
	END;	

ENDIF;


IF( SUBST( Groups , 1 , 2 ) @= 'W_' ); 

	zLO_W=SUBST( Groups , 3 , LONG(Groups) );

	# -- Si l'élément n'existe pas alors on skip
	IF( DIMIX( Dim_LO , zLO_W ) = 0 );
		ITEMSKIP;
	ELSE;
		CellPutS('WRITE', zCube, zLO_W,  Groups );
	ENDIF;

	IF( Subsetexists( Dim_LO , Subset_MDX_W ) = 1 );
		Subsetdestroy( Dim_LO , Subset_MDX_W );
	ENDIF;

	SubsetCreatebyMDX ( Subset_MDX_W , '{TM1DRILLDOWNMEMBER( {TM1FILTERBYPATTERN( {TM1SUBSETALL( [Legal_Organization] )}, "'|zLO_W|'")}, ALL, RECURSIVE )}' );

	nSize_W_LO = SubsetGetSize( Dim_LO, Subset_MDX_W );

	j=1;

	WHILE( j <= nSize_W_LO );
		Elem_W_LO = SubsetGetElementName( Dim_LO , Subset_MDX_W , j );
		CellPutS( 'WRITE' , zCube , Elem_W_LO , Groups );
		j = j + 1;
	END;

ENDIF;


# -- Fin Ajout des modifications liées à la refonte de la sécurité




#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****




#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****

# -- Sous ensemble Source Groups
zDim = '}Groups' ;
SubsetDestroy( zDim , zSubset );

# -- BTA 12/01/2017: Désactivation car doublon dans le process.
# ExecuteProcess('Import_CM_Security_Legal_Organization_2');

Subsetdestroy( Dim_LO , Subset_MDX_R );
Subsetdestroy( Dim_LO , Subset_MDX_W );
#endregion