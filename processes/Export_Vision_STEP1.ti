#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#######################################################
# Project : Tango - Export data for Vision STEP 1 (création de la liste pays pour VISION)
# Created by : RSJC
# Created at : 26/05/2015
# Modified by :
# -- BTA 14/09/2016 : Modification : Suppression des sous ensembles créées en prologue : Ajout de code dans l'épilogue
# Modified at : 
# Modify reason : 
#######################################################

zCube = 'RP_PL';
V_PROCESS_NAME = GETPROCESSNAME();
zDimPI = 'Country_List_VISION';


###########################################################################
#	BEGIN: DEBUG FILES 
###########################################################################
IF(pDEBUG<>0);
	
	V_TIMESTAMP=TIMST(NOW, 'Ymd_his');
	V_DBG_FOLDER=CELLGETS('Z_ADMIN_PARAM','REP_TEMP','STR_VAR1');
	#--REM= Definition of the names of the files of LOG for the DEBUG mode ( one file per process tab).
	V_DBG_FILE_PROLOG=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_PROLOG.txt';
	V_DBG_FILE_METADATA=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_METADATA.txt';
	V_DBG_FILE_DATA=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_DATA.txt';
	V_DBG_FILE_EPILOG=V_DBG_FOLDER|'DBG_'|V_PROCESS_NAME|'_'|V_TIMESTAMP|'_EPILOG.txt';

ENDIF;

###########################################################################
#	END: DEBUG FILES 
###########################################################################


V_TIMESTAMP=TIMST(NOW, '\Y\m\d_\h\i\s');
V_VIEW = 'TMP_VIEW_' | zCube | '_' | V_PROCESS_NAME | '_' | pPeriod | '_' | V_TIMESTAMP;
V_SUBSET = 'TMP_SUBSET_' | zCube | '_' |  V_PROCESS_NAME | '_' | pPeriod | '_' | V_TIMESTAMP;

#-- Création de la dimension  'Country_List_VISION'
IF(DimensionExists( zDimPI ) = 0 ) ;
   DimensionCreate( zDimPI ) ;
ENDIF;

IF(DIMSIZ (zDimPI) > 0 );
DimensionDeleteAllelements( zDimPI) ;
ENDIF;


###  Création de la vue V_VIEW
IF(ViewExists(zCube,V_VIEW)=1);
	ViewDestroy(zCube,V_VIEW);
ENDIF;

ViewCreate(zCube,V_VIEW);


#-- Création du sous-ensemble Legal_Organization
IF( SubsetExists('Legal_Organization' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Legal_Organization' , V_SUBSET);
ENDIF;

SubsetCreateByMDX(V_SUBSET, '{ ORDER( {TM1FILTERBYLEVEL( {TM1SUBSETALL( [Legal_Organization] )}, 0)}, [Legal_Organization].[Country_entity], ASC ) }');
V_SUBSET_SIZE = SubsetGetSize( 'Legal_Organization', V_SUBSET);
SubsetElementInsert( 'Legal_Organization', V_SUBSET, DIMNM( 'Legal_Organization', 1), V_SUBSET_SIZE + 1);
SubsetElementDelete( 'Legal_Organization', V_SUBSET, V_SUBSET_SIZE + 1);


ViewSubsetAssign( zCube, V_VIEW, 'Legal_Organization', V_SUBSET);


#-- Création du sous-ensemble Activity
IF( SubsetExists( 'Activity' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Activity' , V_SUBSET);
ELSE;
    SubsetCreate( 'Activity' , V_SUBSET);
ENDIF;
i=1;
WHILE ( i < DIMSIZ( 'Activity' ) +1);
    ElemP = DIMNM ( 'Activity' , i );
    IF( ELLEV( 'Activity' , ElemP) = 0 & ELISANC('Activity', 'ACT_PMM', Elemp)=1);
        SubsetElementInsert( 'Activity' , V_SUBSET, ElemP , 1 ) ;
    ENDIF;
 i=i+1;
END;

ViewSubsetAssign( zCube, V_VIEW, 'Activity' , V_SUBSET);


#-- Création du sous-ensemble Currency
IF( SubsetExists( 'Currency' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Currency' , V_SUBSET);
ELSE;
    SubsetCreate( 'Currency' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Currency' , V_SUBSET, 'LCL' , 1 ) ;
ViewSubsetAssign( zCube, V_VIEW, 'Currency' , V_SUBSET);



#-- Création du sous-ensemble Gaap
IF( SubsetExists( 'Gaap' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Gaap' , V_SUBSET);
ELSE;
    SubsetCreate( 'Gaap' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Gaap' , V_SUBSET, 'Total_conso', 1 ) ;

ViewSubsetAssign( zCube, V_VIEW, 'Gaap' , V_SUBSET);


#-- Création du sous-ensemble Integration_Rate
IF( SubsetExists( 'Integration_Rate' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Integration_Rate' , V_SUBSET);
ELSE;
    SubsetCreate( 'Integration_Rate' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Integration_Rate' , V_SUBSET, 'NO_APP' , 1 ) ;

ViewSubsetAssign( zCube, V_VIEW, 'Integration_Rate' , V_SUBSET);


#-- Création du sous-ensemble Management_Organization
IF( SubsetExists('Management_Organization' , V_SUBSET) =1 );
    SubsetDeleteAllElements('Management_Organization' , V_SUBSET);
ENDIF;

#Sélection uniquement des enfants des pays dont l'attribut PMM_Country = "PMM_Country"
SubsetCreateByMDX(V_SUBSET,'{TM1FILTERBYLEVEL( {TM1DRILLDOWNMEMBER( {EXCEPT( {FILTER( {TM1SUBSETALL( [Management_Organization] )}, [Management_Organization].[PMM_Country] = "PMM_Country")}, {[Management_Organization].[FR] }) }, ALL, RECURSIVE )}, 0)}');
V_SUBSET_SIZE = SubsetGetSize( 'Management_Organization', V_SUBSET);
SubsetElementInsert( 'Management_Organization', V_SUBSET, DIMNM( 'Management_Organization', 1), V_SUBSET_SIZE + 1);
SubsetElementDelete( 'Management_Organization', V_SUBSET, V_SUBSET_SIZE + 1);

ViewSubsetAssign( zCube, V_VIEW, 'Management_Organization', V_SUBSET);



#-- Création du sous-ensemble Period
IF( SubsetExists( 'Period' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Period' , V_SUBSET);
ELSE;
    SubsetCreate( 'Period' , V_SUBSET);
ENDIF;

#Paramètre de la période
IF(pPeriod@='Default');
	V_PERIOD_SRC = CellGetS( 'z_Admin_Param' , 'MONTH_ACTUAL' , 'STR_VAR1') | '_YTD';
ELSE;
	V_PERIOD_SRC = pPeriod;
ENDIF;

SubsetElementInsert( 'Period' , V_SUBSET, V_PERIOD_SRC , 1 ) ;
  
ViewSubsetAssign( zCube, V_VIEW, 'Period' , V_SUBSET);



#-- Création du sous-ensemble Phase
IF( SubsetExists( 'Phase' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Phase' , V_SUBSET);
ELSE;
    SubsetCreate( 'Phase' , V_SUBSET);
ENDIF;
SubsetElementInsert( 'Phase' , V_SUBSET, 'ACT_TOT', 1 ) ;

#pPHASE=CELLGETS('Z_ADMIN_PARAM','FORECAST_VERSION_VISION','STR_VAR1');
#Extraction de la phase prévisionnelle précédente
pPHASE=CELLGETS('Z_ADMIN_PARAM','MONTH_BUDGET_FORECAST','STR_VAR1');

                IF(SUBST(pPHASE,6,2)@='01'); pPHASE='FC_2_VC';ENDIF;
                IF(SUBST(pPHASE,6,2)@='05'); pPHASE='BUDG_VC';ENDIF;
                IF(SUBST(pPHASE,6,2)@='10'); pPHASE='FC_1_VC';ENDIF;

SubsetElementInsert( 'Phase' , V_SUBSET, pPhase, 1 ) ;

ViewSubsetAssign( zCube, V_VIEW, 'Phase' , V_SUBSET);


#-- Création du sous-ensemble Indicator
IF( SubsetExists( 'Indicator' , V_SUBSET) =1 );
    SubsetDeleteAllElements( 'Indicator' , V_SUBSET);
ELSE;
    SubsetCreate( 'Indicator' , V_SUBSET);
ENDIF;
i=1;
WHILE( i < DIMSIZ( 'Indicator' ) + 1 ) ;
    ElemP = DIMNM( 'Indicator' , i );
    IF(ATTRS('Indicator',ElemP, 'VISION')  @= 'VISION'); 
        SubsetElementInsert( 'Indicator' , V_SUBSET, ElemP , 1 ) ;
    ENDIF;
i=i+1;
END;

ViewSubsetAssign( zCube, V_VIEW, 'Indicator' , V_SUBSET);


#-- Mise à jour des paramètres de la vue
ViewExtractSkipZeroesSet ( zCube, V_VIEW, 1 );
ViewExtractSkipRuleValuesSet ( zCube, V_VIEW, 0 );
ViewExtractSkipCalcsSet ( zCube, V_VIEW, 0 );

DataSourceNameForServer = zCube;
DataSourceCubeView = V_VIEW;



#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****

vCountry = ATTRS('Legal_Organization',V_Legal_Organization,'Country_entity');



#V_TXT_FILE=CellGets('Z_ADMIN_PARAM','REP_OUTPUT','STR_VAR1') | '\Vision' | '\' | vCountry | '_' | V_TIMESTAMP|'.csv';

# Alimentation de la liste des pays PMM pour VISION
DimensionElementInsert ( zDimPI , '' , vCountry , 'n' ) ;





#vHeader=1;


#END;
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****



#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****



# Détermination du paramètre pays
i=1;
WHILE( i < DIMSIZ ( zDimPI )+1 );
     Country = DIMNM (zDimPI, i);
     Return_Value = ExecuteProcess( 'Export_Vision_STEP2' , 'pCountry' , Country );
     i = i + 1;
END;




# -- BTA 14/09/2016 : Suppression des sous ensembles et de la vue
IF(pDEBUG=0);

	IF(ViewExists(zCube,V_VIEW)=1);
		ViewDestroy(zCube,V_VIEW);
	ENDIF;

	#-- Destruction du sous-ensemble Legal_Organization
	SubsetDestroy('Legal_Organization' , V_SUBSET);

	#-- Destruction du sous-ensemble Activity
	SubsetDestroy('Activity' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Currency
	SubsetDestroy('Currency' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Gaap
	SubsetDestroy('Gaap' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Integration_Rate
	SubsetDestroy('Integration_Rate' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Management_Organization
	SubsetDestroy('Management_Organization' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Period
	SubsetDestroy('Period' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Phase
	SubsetDestroy( 'Phase' , V_SUBSET) ;

	#-- Destruction du sous-ensemble Indicator
	SubsetDestroy( 'Indicator' , V_SUBSET) ;

ENDIF;
#endregion