#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

########################################
# Project : Tango - Data Load -> Transco Cube TC_PHASE_VTD_VECTOR
# Created by : NEK
# Created at : 07/11/2011
# Modified by :
# Modified at :
# Modify reason :
#########################################

zCube= 'TC_PHASE_VTD_VECTOR';
CubeSetLogChanges(zCube, 0);
zCube_Reject='ZZ_PROCESS_REJECT';
zCube_Process='ZZ_PROCESS_DETAIL';
nb_lign=0;
nb_reject=0;
nb_load=0;
zSource = 'VTD_VECTOR';
zProcess='DL_TC_PHASE_VTD_VECTOR';
zDateLoadingStart = TIMST(now,'\Y-\M-\D');
zDateTimeLoadingStart = TIMST(now,'\Y-\M-\D \h:\i:\s');

Source_File = CellGetS( 'z_Admin_Param' , 'REP_ITF' , 'STR_VAR1') | '\Phase_VTD_VECTOR.csv';
DataSourceNameForServer = Source_File;

IF (FileExists (Source_File) =0);
     CubeSetLogChanges(zCube, 1);
     ItemReject(  DataSourceNameForServer | ' is not find' );
     ProcessQuit;
ENDIF;

#################################################################################
#                                                               Clear CUBE
#################################################################################

P_NAME_RAZ = zCube | '_RAZ';
ViewDestroy( zCube , P_NAME_RAZ );
ViewCreate( zCube , P_NAME_RAZ );

#-- Clear cube
ViewZeroOut( zCube , P_NAME_RAZ );

#-- Update subset
ViewExtractSkipZeroesSet ( zCube , P_NAME_RAZ , 1 );
ViewExtractSkipRuleValuesSet ( zCube , P_NAME_RAZ , 1 );
ViewExtractSkipCalcsSet ( zCube , P_NAME_RAZ , 1 );

#-- Delete subset
ViewDestroy( zCube , P_NAME_RAZ );


#################################################################################
#                                                               Clear Reject Cube
#################################################################################

######
#Call process DB_zz_Date_Time_loading in order to add the new day in the dimension zz_Date_Time_loading
ExecuteProcess('DB_zz_Date_Time_loading');
######

P_NAME_RAZ = zCube_Reject | '_RAZ';
ViewDestroy( zCube_Reject , P_NAME_RAZ );
ViewCreate( zCube_Reject , P_NAME_RAZ );

#-- Create subset in zz_Date_Time_loading and }Processes
IF( SubsetExists( 'zz_Date_Time_loading' , P_NAME_RAZ ) =1 );
    SubsetDeleteAllElements( 'zz_Date_Time_loading' , P_NAME_RAZ);
ELSE;
    SubsetCreate( 'zz_Date_Time_loading' , P_NAME_RAZ );
ENDIF;

IF( SubsetExists( '}Processes' , P_NAME_RAZ ) =1 );
    SubsetDeleteAllElements( '}Processes' , P_NAME_RAZ);
ELSE;
    SubsetCreate( '}Processes' , P_NAME_RAZ );
ENDIF;

SubsetElementInsert( 'zz_Date_Time_loading' , P_NAME_RAZ ,zDateLoadingStart , 1 );
SubsetElementInsert( '}Processes' , P_NAME_RAZ , zProcess , 1 );

ViewSubsetAssign( zCube_Reject , P_NAME_RAZ , 'zz_Date_Time_loading' , P_NAME_RAZ);
ViewSubsetAssign( zCube_Reject , P_NAME_RAZ , '}Processes', P_NAME_RAZ);

#-- Update subset
ViewExtractSkipZeroesSet ( zCube_Reject , P_NAME_RAZ , 1 );
ViewExtractSkipRuleValuesSet ( zCube_Reject , P_NAME_RAZ , 1 );
ViewExtractSkipCalcsSet ( zCube_Reject , P_NAME_RAZ , 1 );

#-- clear cube
ViewZeroOut( zCube_Reject , P_NAME_RAZ );

#-- Delete subset
ViewDestroy( zCube_Reject , P_NAME_RAZ );
SubsetDestroy( 'zz_Date_Time_loading' , P_NAME_RAZ );
SubsetDestroy( '}Processes' , P_NAME_RAZ );

#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****



# Insert the new Indicator operational
IF( DIMIX( 'z_phase_VTD_Vector' , Vector_phase ) = 0 );
    DIMENSIONELEMENTINSERT( 'z_phase_VTD_Vector' , '' , Vector_Phase , 'n' );
ENDIF;

#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


nb_lign=nb_lign+1;
zRecord = Vector_Phase | ';' |  Vector_Exercice | ';' | Vector_Period | ';' | Tango_phase | ';' | Tango_period;
#################################################################################
#                                                                             REJECT SECTION
#################################################################################
#-- if the tango phase is not exists in phase dimension, this record is reject
IF( Dimix( 'Phase' , Tango_Phase ) = 0 );
   zerror_message='The phase ' | Tango_Phase | '  does not exist in dimension Phase at line ' |  numbertostring(nb_lign) ;
   nb_reject=nb_reject+1;
   CellPuts(zrecord,  zCube_Reject , zProcess , zDateLoadingStart , 'l' | numbertostring(nb_reject) , 'Record');
   CellPutS(zerror_message , zCube_Reject , zProcess , zDateLoadingStart , 'l' | numbertostring(nb_reject), 'Error_Message');
   ItemSkip;
ENDIF;

#-- if the tango period is not exists in period_YTD dimension, this record is reject
IF( Dimix( 'Period_YTD' , Tango_Period ) = 0 );
   zerror_message='The period ' | Tango_Period | '  does not exist in dimension Period_YTD at line ' |  numbertostring(nb_lign) ;
   nb_reject=nb_reject+1;
   CellPuts(zrecord,  zCube_Reject , zProcess , zDateLoadingStart , 'l' | numbertostring(nb_reject) , 'Record');
   CellPutS(zerror_message , zCube_Reject , zProcess , zDateLoadingStart , 'l' | numbertostring(nb_reject), 'Error_Message');
   ItemSkip;
ENDIF;

#####################################################################################"
#                                                                             INPUT SECTION
#################################################################################

#-- Input dans le cube TC_PHASE_VTD_VECTOR
IF( Dimix( 'Phase' , Tango_Phase ) <> 0  & Dimix( 'Period_YTD' , Tango_Period ) <> 0);
     CellPutS( Tango_Phase , zCube , Vector_phase, vector_exercice,vector_period, 'STR_VAR1' );
     CellPutS( Tango_Period , zCube , Vector_phase, vector_exercice,vector_period, 'STR_VAR2' );
     nb_load=nb_load+1;
ENDIF;


#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****



CubeSetLogChanges(zCube, 1);
#-- Copy the file structure in historical directory
ExecuteProcess( 'z_Tools_Structure_Transfert' , 'zFile' , 'Phase_VTD_Vector' );


zDateTimeLoadingEnd = TIMST(now,'\Y-\M-\D \h:\i:\s');
pCountry=CellGetS( 'z_Admin_Param' , 'COUNTRY' , 'STR_VAR1');

#################################################################################
#                                                                             PROCESS with PERIOD and INSTANCE
#################################################################################
if (pCountry@<>'CM');

zCube_Process_PP = 'ZZ_PROCESS_Detail_Instance';
pChore='Task2_CO_Load_Common_Dimension_' | pCountry;
pProcess_source=zsource;
pPeriod=CellGetS( 'z_Admin_Param' , 'MONTH_ACTUAL' , 'STR_VAR1');
CellPutS(zDateTimeLoadingStart , zCube_Process_PP, pChore, zProcess ,pperiod,zDateLoadingStart, pCountry,pProcess_source ,  'Start_date');
CellPutS(zDateTimeLoadingEnd , zCube_Process_PP, pChore, zProcess ,pperiod,zDateLoadingStart, pCountry ,pProcess_source, 'End_date');
CellPutS('OK' ,zCube_Process_PP, pChore, zProcess ,pperiod,zDateLoadingStart, pCountry ,pProcess_source,   'Status');
CellPutS(numbertostring(nb_lign) , zCube_Process_PP, pChore, zProcess , pperiod ,zDateLoadingStart, pCountry,pProcess_source , 'Nb_Input_records')
;
CellPutS(numbertostring(nb_reject) , zCube_Process_PP, pChore, zProcess ,pperiod ,zDateLoadingStart, pCountry,pProcess_source  , 'Nb_reject_recor
ds');
CellPutS(numbertostring(nb_load) , zCube_Process_PP, pChore, zProcess ,pperiod ,zDateLoadingStart, pCountry,pProcess_source  , 'Nb_load_records')
;
IF(nb_lign = nb_load);
    CellPutS('OK' ,zCube_Process_PP, pChore, zProcess ,pperiod ,zDateLoadingStart, pCountry ,pProcess_source,   'Status');
else;
    CellPutS('KO' ,zCube_Process_PP, pChore, zProcess ,pperiod ,zDateLoadingStart, pCountry ,pProcess_source,   'Status');
endif;

ENDIF;

#################################################################################
#                                                                             DETAIL PROCESS SECTION
#################################################################################

CellPutS(zDateTimeLoadingStart , zCube_Process, zProcess ,zDateLoadingStart , 'Start_date');
CellPutS(zDateTimeLoadingEnd , zCube_Process, zProcess ,zDateLoadingStart  , 'End_date');
CellPutS(numbertostring(nb_lign) , zCube_Process, zProcess ,zDateLoadingStart  , 'Nb_Input_records');
CellPutS(numbertostring(nb_reject) , zCube_Process, zProcess ,zDateLoadingStart  , 'Nb_reject_records');
CellPutS(numbertostring(nb_load) , zCube_Process, zProcess ,zDateLoadingStart , 'Nb_load_records');

IF(nb_lign = nb_load);
    CellPutS('OK' , zCube_Process, zProcess ,zDateLoadingStart , 'Status');
else;
    CellPutS('KO' , zCube_Process, zProcess ,zDateLoadingStart , 'Status');
    ItemReject( ' Process exited with errors at ' | TIME |  ' on ' | TODAY | '=> Check cubes : zz_Process_Detail and  zz_Process_Reject' );
endif;

#################################################################################
#                                                                             END PROCESS
#################################################################################
#endregion